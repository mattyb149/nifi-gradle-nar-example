plugins {
    id 'org.ajoberstar.grgit' version '2.1.0'
}

description = 'An example project on how to use Gradle to build NiFi ARchive projects (NARs)'

final String BUILD_TIMESTAMP_FORMAT = "yyyy-MM-dd'T'HH:mm:ss'Z'";

// Populate this if your NAR has a parent NAR
def parentNar = null
        //[group  : 'org.apache.nifi', name   : 'nifi-standard-services-api-nar', version: project.version ]

jar {
    dependsOn ':nifi-gradle-built-example-processors:jar'
    jar.extension = 'nar'

    into('META-INF/bundled-dependencies') {
        from project(':nifi-gradle-built-example-processors').configurations.compile
        from project(':nifi-gradle-built-example-processors').tasks['jar'].archivePath
    }

    manifest {
        def defaultAttrs = [
                'Build-Revision'                     : getWorkingRevision() ?: 'unknown',
                'Build-Branch'                       : getWorkingBranch() ?: 'unknown',
                'Build-Tag'                          : getWorkingTag(),
                'Build-Timestamp'                    : new Date().format(BUILD_TIMESTAMP_FORMAT),
                'Built-By'                           : System.properties['user.name'] ?: 'unknown',
                'Nar-Id'                             : project.name,
                'Nar-Version'                        : project.version,
                'Clone-During-Instance-Class-Loading': false,
                'Created-By'                         : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'                          : "Groovy ${GroovySystem.version}"
        ]
        if (parentNar) {
            defaultAttrs += [
                    'Nar-Dependency-Group'  : parentNar.group,
                    'Nar-Dependency-Id'     : parentNar.name,
                    'Nar-Dependency-Version': parentNar.version
            ]
        }
        attributes(defaultAttrs)
    }
}

def getWorkingBranch() {
    grgit.branch.getCurrent().name
}

def getWorkingRevision() {
    grgit.head().id
}

def getWorkingTag() {
    grgit.tag.list()[0] ?: 'HEAD'
}